plugins {
    id 'java'
    id 'application'    
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
}

group = 'com.aadityadesigners.tutorials'
version = '1.0.0'
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
bootJar {
    archiveBaseName.set('app')
}
application {
    // Define the main class for the application.
    mainClass = 'com.aadityadesigners.tutorials.App'
}


repositories {
    mavenCentral()
}
dependencies {
    // SPRING: WEB
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    configurations.all {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'ch.qos.logback', module: 'logback-core'        
    }     
    // third_party_logging: Log4J2 logging
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'com.lmax:disruptor:3.4.4'

    /** SECURITY **/
    // spring cloud: vault
    implementation 'org.springframework.cloud:spring-cloud-starter-vault-config'
    // OAuth 2.0
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    /** TOOLS **/
    // Jackson for Java-JSON conversion
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'

    // Lombok for auto getter/setters
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Apache Commons
    implementation 'org.apache.commons:commons-lang3'
    implementation 'commons-io:commons-io:2.11.0'

    // SPRING: Testing
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    // DEVELOPMENT TOOL
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2022.0.4"
    }
}

